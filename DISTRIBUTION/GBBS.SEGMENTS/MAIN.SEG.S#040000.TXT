; *******************
; GBBS "Pro" V:2.3
; Copyright 1980-2023
; Kevin M. Smallwood
; *******************

; main segment - 1/11/2023

 public fromseg
 public return
 public term1

 on nocar goto term1
 on error goto error

 if flag(0) goto main
 if fv then a$="b:v1":gosub force

fromseg
 on nocar goto term1
 on error goto error

main
 clear key:gosub time
 print \"["x$"]["on$"Main Level"of$"] Option (?=Help)";
 l1$="BCDEFHIPRT$?":if (flag(1) or info(5)) l1$=l1$+"GLMOQSUVX%"
 gosub getone:push main

main.cmd
 if i$="B" print "Bulletin Boards":bb=1:link "a:msg.seg","bulletins"
 if i$="C" print "Chat":goto chat
 if i$="D" print "Display Characteristics":goto display
 if i$="E" print "Show Your Status":goto show.stat
 if i$="F" print "Feedback":goto feedback
 if i$="H" print "Help":f$="b:hlp.main":goto show.file
 if i$="I" print "System Info":f$="b:sys.info":goto show.file
 if (i$="P") and (flag(0)) print "Get a Password":goto getpass
 if (i$="R") and (not flag(0))  print "Read Mail":pop:link "a:mail.seg","mailread"
 if i$="T" print "Terminate":goto terminate
 if i$="$" print "System News": f$="b:sys.news":goto show.file
 if (i$="%") and (info(5)) pop:print "System":link "a:system.seg"
 if i$="?" or i$="/" print "Menu":goto menu

; below commands for info(5) or flag(1) users only

 if i$="G" print "General Files":a$="c:g1":goto general
 if i$="L" print "Caller Log":goto showlog
 if i$="M" print "Mail":pop:link "a:mail.seg"
 if i$="O" print "Other Boards":f$="b:bbs":goto show.file
 if i$="P" print "Change Password":goto setpass
 if i$="Q" print "Quickscan":bb=1:link "a:msg.seg","bulletins"
 if i$="U" print "View Userlist":goto userlist
 if i$="V" print "Vote":a$="b:v1":goto voting
 if i$="X" print "File Transfers":a$="d:d1":goto download

; On the fly menus
menu
 a1=0:f$="b:mnu.new"
 if not (flag(1)) goto menu.1
 if (edit(3)<79) f$="b:mnu.val.40":else f$="b:mnu.val.80"
menu.1
 open #1,f$:input #1,x$:setint(" "):for l=1 to len(x$)
 addint(mid$(x$,l,1)):next:print \sc$\
 copy #1:if key(3) goto menu.key
 a=key(0):close:setint(""):pop:goto main

menu.key
 close:setint(""):a=key(0)
 if (a>96) and (a<123) a=a-32
 if a=32 pop:goto main
 print:i$=chr$(a):print i$:goto main.cmd

; terminate from system and recycle
terminate
 print \"Terminate from "sn$
 print \"Are you sure";:yn=2:gosub getyn
 if i$<>"Y" return
term1
 on nocar clear
 on error goto error
 print \"Log Out : "a3$" of "a4$", "a5$"."
 print "Caller #: "cn$\
 print sn$ " - log out at"\fmtdate$" "time$
 a=clock(1)
 if a print \"Connected "a/60" mins, "a mod 60" secs"

 close:clear #8
 a=0:open #1,"b:top.uploaders"
 for x=1 to 10:input #1,i$,a1,a2
 if (a3$=i$) and (u2=a2) x=10:next:goto nowrite
 if (u1>a1) and not(a) print #8,a3$,u1,u2:a=1:goto top1
 if (u1=a1) and (u2>a2) and not(a) print #8,a3$,u1,u2:a=1
top1
 if a3$=i$ next:goto top2
 print #8,i$,a1,a2:next
top2
 close:if not(a) goto nowrite
 kill "b:top.uploaders":create "b:top.uploaders"
 open #1,"b:top.uploaders":copy #8,#1:close
 a=x
 print \"By the way..."
 print "You are one of the Top 10 Uploaders!"

nowrite
 print \md$\'            GBBS "Pro" V:2.3'
 print "      (C)1986-2023 Kevin Smallwood" \md$

 close:modem(1):if a1$="" goto term4
 if not un goto term3

term2
 when$="x":tc=tc+1
 nibble(6)=nibble(6)+1:if cm$<>date$ nibble(6)=0
 byte(1)=tc mod 256:nibble(1)=tc/256
 byte(2)=bp mod 256:nibble(2)=bp/256
 byte(3)=dl mod 256:nibble(3)=dl/256
 byte(4)=ul mod 256:nibble(4)=ul/256
 byte(5)=lr mod 256:byte(6)=lr/256
 byte(0)=info(3)

 byte(10)=u1 mod 256:byte(11)=u1/256
 byte(12)=u2 mod 256:byte(13)=u2/256
 byte(14)=d1 mod 256:byte(15)=d1/256
 byte(16)=d2 mod 256:byte(17)=d2/256

; to enable the time per day mod remove the ';' from the line below
; byte(7)=(byte(7)+(clock(1)/60))

 open #1,"b:users"
 position #1,128,un
 print #1,a1$,a2$\a3$\a4$,a5$
 position #1,128,un,70
 write #1,ram,58:close

 kill "h:batch"

term3
 if not info(5) then ct=ct+1:c2=c2+1
 if c2>9999 then c2=0:c1=c1+1
 open #1,"b:data":print #1,c1,c2,ct,ct$,date$
 print #1,nu,mn,wm,tm\a3$:close
 if (lg=0) or (info(5)) goto term4

; write to system log
 create "b:log.system":open #1,"b:log.system":append #1
 print #1,"Call #"cn$" / Connected at "cm$" / "a3$" of "a4$", "a5$
 print #1,"Last date on "lc$" / "ph$" / "un"-"pa$;
 if info(2)=128 x$="38400":else x$=str$(info(2)*300)
 print #1," / Speed = "x$" baud"
 for x=1 to 8:print #1,flag(x);:next:a=clock(1)
 print #1," / Connected "a/60" mins, "a mod 60" secs"\:close

; write to caller log
 f$="b:log.caller":create f$:open #1,f$
 input #1,x
 position #1,80,x
 print #1,a/60,a mod 60
 close

term4
 link "a:logon.seg"
term5
 ma=0:goto term1

; chat with sysop
chat
 if ch>5 print \"You were warned.  Goodbye!":goto term1
 if ch>4 print \"Once more and you'll be logged off":ch=ch+1
 if ch>3 print \"Sysop is not available":ch=ch+1:return
 print \"Paging Sysop: ";:tone(100,100,100)
 if flag(2) tone (125,100,100):tone(150,100,100)
 print "Continue...":info(4)=1:ch=ch+1:return

; show user's status
show.stat
 print \"   "on$"Your Status"of$\
 print "Name -> "a3$
 print "From -> "a4$", "a5$
 print "Phone # "ph$
 print "User  # "un
 print "Last On : "lc$
 print "Level # : ";
 for x=1 to 8:print flag(x);:next
 print \\"Last Caller: "lu$
 print \"You are caller : "cn$
 print "Todays  calls : "ct\
 print "Logon time : "cm$
 print "Actual time: "date$" "time$:a=clock(1)
 print "Connected  : "a/60" mins, "a mod 60" secs"
 b=clock(2):if b print "Time left  : "(b-a)/60" mins"
 print \"[ Options Available ]"\
 open #1,"b:data2":z=0:for x=0 to 34:a$=""
 if flag(x) position #1,32,x:input #1,a$
 if a$<>"" setint(1):print a$:z=1:if key(1) then x=34
 next:close:if not z print "Limited system access"
 setint(""):return

; show list of system users
userlist
 print \"List of System Users"\
 input @2 "Match letters (<CR>=All,?):" i$
 if i$="?" f$="b:hlp.user":gosub show.file:goto userlist
 open #1,"b:users":x=1
 if left$(i$,1)="#" x=val(mid$(i$,2)):i$=""
 if (x=0) or (x>nu) then x=1

; print header
 print \"###  User's Name             Last  On  ";:if edit(3)<79 print
 print "From                  ";
 if info(5) print "Pass    ":else print
 print chr$(45,39);:if edit(3)<79 print:goto usrlst2
 print chr$(45,22);
 if info(5) print chr$(45,8):else print

usrlst2
 position #1,128,x:input #1,a$,b$
 if a$="" goto usrlst3
 a$=a$+" "+b$:setint(1)
 if not instr(i$,a$) goto usrlst3
 input #1,c$\d$,e$:position #1,128,x,70
 move ram,58 to ram2:on nocar goto usrlst4
 read #1,ram,58
 setint(1):print right$("  "+str$(x),3)"  ";
 print left$(c$+chr$(32,22),22)"  ";when$"  ";:if edit(3)<79 print
 print left$(d$+", "+e$+chr$(32,20),20)"  ";
 if info(5) print pa$:else print
 move ram2,58 to ram:on nocar goto term1
usrlst3
 if (not key(1)) and (x<nu) then x=x+1:goto usrlst2
 setint(""):close:return
usrlst4
 move ram2,58 to ram:goto term1

showlog
 f$="b:log.caller":open #1,f$:if mark(1) goto shlog2
 print \s$\\"###  Username"chr$(32,26);:if edit(3)<79 print
 print "Login Time            Online  Baud"
 print chr$(45,39);:if edit(3)>40 print chr$(45,35):else print
 input #1,x:y=x:z=1
shlog1
 position #1,80,x:if eof(1) goto shlog2
 input #1,f$,g$
 position #1,80,x,10
 input #1,a$,b$,c$
 setint(1)
 print right$("00"+str$(z),3)"  "left$(a$+chr$(46,32),32)"  ";
 if edit(3)<79 print
 print left$(b$+chr$(46,20),20)"  ";
 if c$="0" c$="LOCAL":else c$=right$("00"+c$,5)
 print right$("0"+f$,2)":"right$("0"+g$,2)"   "c$
 if edit(3)<79 print
 if key(1) x=y+1
 x=x-1:z=z+1:ifx=0 x=200
 if x<>y goto shlog1
shlog2
 close:setint("")
 return

; file transfer section
download
 print \"Top 10 Uploaders as of "date$\
 open #1,"b:top.uploaders"
 for x=1 to 10:input #1,i$,a1,a2
 z$=str$(a2):if a1 z$=str$(a1)+right$("000"+str$(a2),4)
 print right$(" "+str$(x),2)". "left$(i$+"....................",20)"  "z$" Blocks"
 next:close

; check for supertac
 open #1,"a:supertac.s":a=mark(1):close
 if not a pop:link "a:supertac"

 open #1,a$:input #1,x,d3$:close
 if d3$<>"" d3$=chr$(13)+d3$+chr$(13)
 b$=left$(a$,instr(":",a$))
down2
 print d3$\"Download Files 1-"x" [L]ist Files"
 input "[U]pload a File [H]elp [Q]uit ? "i$
 a=val(i$):if i$="Q" return
 if i$="U" goto upload
 if i$="L" goto down3
 if i$="H" then f$="b:hlp.xfer":gosub show.file:goto down2
 if (a=0) or (a>x) goto down2
 open #1,a$:input #1,x:y=0
down2a
 if eof(1) close:goto down2
 input #1,j$:if left$(j$,1)<>")" goto down2a
 y=y+1:if a<>y goto down2a:else y=instr("^",j$)
 if y f$=b$+mid$(j$,y+1):else f$=b$+mid$(j$,3)
 close:open #1,f$:if mark(1) close:goto down2
 bi=size(1)*2+1:j$=mid$(j$,3):if y j$=left$(j$,y-4)
 print \"Download "j$", "on$;bi;of$" blocks" 
 print "Press <"on$"CR"of$"> to begin";:l1$=chr$(13):gosub getone
 print \"Sending via Xmodem...":close
 use "b:protocol.down",110,0,f$
 if peek(10)>0 print on$"[Error]"of$"...Transfer Aborted":goto down2
 print on$"Transfer Successful"of$ 
 dl=dl+1:d2=d2+bi:if d2>9999 d1=d1+1:d2=d2-10000
 dl$=str$(d2):if d1 dl$=str$(d1)+right$("000"+str$(d2),4)
 goto down2

down3
 print:open #1,a$:input #1,x:a=1
 if not x close:print \on$"Closed..."of$:goto down2
down4
 input #1,j$:z=instr("^",j$):if z=0 z=len(j$)+1
 if left$(j$,1)=")" print a;:a=a+1:j$=left$(j$,z-1)
 setint(1):print j$:if not(eof(1) or key(1)) goto down4
 close:setint(""):goto down2

upload
 if not flag(2) print \"Security too low":goto down2
 print \"Upload a file"
 y=1:f$=""
upload2
 input @2 \"Filename:" f$:if f$="" goto upload
 if not info(5) then f$="e:u"+str$(un)+"."+f$
 if not instr(":",f$) then f$="e:"+f$
 if len(f$)>17 print \on$"Illegal filename"of$:goto upload2
 open #1,f$:a=mark(1):close:if a goto upload3
 if info(5)=0 print \on$"Filename in use"of$:goto upload2
 print \on$"Filename in use:"of$" Overwrite";:yn=2:gosub getyn
 if i$<>"Y" goto upload2
upload3
 create f$:open #1,f$:a=mark(1):close
 if a print \on$"Illegal filename"of$:goto upload2
 print "Press <"on$"CR"of$"> to begin";:l1$=chr$(13):gosub getone
 print \"Receiving via Xmodem...":close
 use "b:protocol.up",110,0,f$
 if peek(10)>0 print \on$"[Error]"of$"...Transfer Aborted":goto down2
 open #1,f$:a=mark(1):if a print \on$"[Error]"of$"...Transfer Aborted":close:goto down2
 bi=size(1)*2+1:close
 print \"Received "on$;bi;of$" blocks.":close
 print on$"Transfer Successful"of$ 
 ul=ul+1:u2=u2+bi:if u2>9999 u1=u1+1:u2=u2-10000
 ul$=str$(u2):if u1 ul$=str$(u1)+right$("000"+str$(u2),4)
 goto down2

; feedback to sysop
feedback
 print \"Feedback to Sysop"\
 f$="feedback":gosub chkbox
 ready mb$:if msg(0)=128 print "Sorry, mailbox full.":return
 input @3 "Subject -> "sb$:if sb$="" sb$="None"
 rg=0:sb$=left$(sb$,25):st$="Awaiting Reply"
 h$="     --> Feedback from a User <--"
 gosub editor:if not edit(2) return
 print \"Wait...";:ready mb$:a=msg(0)+1
 print #msg(a),date$" "time$\a3$" (#"un")",un\st$\sb$\rg
 print #6,h$\
 copy #8,#6:msg(a)=a:update:ready " ":print ".email sent"
 return

; general files
general
 open #1,a$:input #1,x
 if not x close:print \on$"Closed..."of$:return
 print \s$\:setint(1):gosub showfl2
general2
 print \"Which 1-"x" (?=Menu,<CR>):";
 input @3 i$:if i$="" return
 if i$="?" goto general
 a=val(i$):if (not a) or (a>x) goto general2
 f$=a$+"."+str$(a):gosub show.file:goto general2

; set display characteristics
display
 print \"Video width now = "edit(3)+1
 print "Back-space mode = ";
 a=nibble(0)/4:if a=1 print "non-";
 if a=0 print "Unknown":else print "destruct"
 print "Nulls sent @ LF = "info(3)
 print "Page pause mode = ";
 if flag(35) print "ON":else print "OFF"
 print "Show phone numb = ";
 if mid$(ph$,4,1)=" " print "YES":else print "NO"
display2
 print \"Set: [V, B, N, P, S, or Q=Quit]";:l1$="BNPQSV"+chr$(13):gosub getone
 if i$="Q" or i$=chr$(13)print "Quit":return
 if i$="B" print "Backspace Mode":goto backspace
 if i$="S" print "Show Phone":goto phone
 if i$="N" print "Nulls":goto nulls
 if i$="P" print "Page Pause":goto paging
 if i$="V" print "Set Video Width":goto video
 goto display2

video
 print \"New width (";
 print width(1),width(2),width(3),width(4);
 input @2 "): " i$:if i$="" goto display
 a=val(i$):for x=1 to 4
 if a=width(x) nibble(0)=nibble(0)/4*4+(x-1):edit(3)=a-1
 next:print \"Video width = "edit(3)+1:goto display

backspace
 print \"123456" chr$(8,3):a=0
 print \"How many numbers do you see (3 or 6)";
 l1$="36"+chr$(13):gosub getone
 if i$=chr$(13) goto display
 if i$="6" a=1
 if i$="3" a=2
 nibble(0)=(nibble(0) mod 4)+(a*4)
 edit(4)=a:goto display

nulls
 input @2 \"Set new value to (0-127):" i$
 if i$<>"" then info(3)=val(i$)
 goto display

phone
 print \"Show phone number to other users";:yn=2:gosub getyn
 if i$="Y" ph$=left$(ph$,3)+" "+mid$(ph$,5):goto display
 if i$="N" ph$=left$(ph$,3)+"-"+mid$(ph$,5):goto display

paging
 input @2 \"Set paging (ON,OFF):" i$
 if i$="ON" flag(35)=1
 if i$="OFF" flag(35)=0
 goto display

; get a password for guests
getpass
 print \"Do you wish to receive a password to"
 print "log in with the next time you call";:yn=2:gosub getyn
 if i$<>"Y" return
 d1$=a1$:d2$=a2$:d3$=a3$
 link "a:logon.seg","get.pass"

; get a new password
setpass
 print \"Change your Password"
 input @2 \"Enter your current password: "; i$
 if i$="" return
 i$=left$(i$+"    ",8):if i$<>pa$ print \"Incorrect!":return
 print \"Your password may be 4-8 chars long"
 echo="X":input @2 \"Enter your new password: "; i$
 if i$="" then echo="":return
 input @2 "Please type it in again: " a$
 echo="":a=len(i$)
 if a$<>i$ print \on$"Passwords do not match"of$:return
 if (a<4) or (a>8) print \on$"Password must be 4-8 chars"of$:return
 pa$=left$(i$+"    ",8):print \"New Password Accepted":return

; do voting section
force
 if not flag(1) then return
 open #1,a$:mark(1)=(un/512)*64:fill ram2,64,0
 read #1,ram2,64:z=flag:flag=ram2:a=flag(un)
 flag=z:close:if a return

voting
 setint(""):create a$:open #1,a$
 mark(1)=(un/512)*64:x=mark(1):fill ram2,64,0
 read #1,ram2,64:z=flag:flag=ram2:a=flag(un)
 flag(un)=1:flag=z:mark(1)=x:write #1,ram2,64
 x=1:z=byte:byte=ram2
vote2
 open #2,a$+"."+str$(x):if mark(2) goto vote5
 input #2,y:if a then setint(1)
 print \s$\:copy #2:position #1,32,x+7
 fill ram2,32,0:read #1,ram2,32
 b=byte(0)+byte(1)*256:if a goto vote4
vote3
 print \"Vote (1-"y",S=Skip):";
 input i$:if i$="S" goto vote4
 d=val(i$):if (d<1) or (d>y) goto vote3
 b=b+1:byte(0)=b mod 256:byte(1)=b/256
 c=byte(d*2)+byte(d*2+1)*256:c=c+1
 byte(d*2)=c mod 256:byte(d*2+1)=c/256
 position #1,32,x+7:write #1,ram2,32
vote4
 if key(1) then close:return
 print \"Results from "b" users:"\:b=b+(b=0)
 for c=1 to y:y=byte(c*2)+byte(c*2+1)*256
 print "Answer "c" - "(y*100)/b"%"
 next:close #2:x=x+1:goto vote2
vote5
 close:byte=z:return

return
 on nocar goto term1
 on error goto error
 return

; *** sub - routines ***

;check mailbox status and create if not present
chkbox
 mb$="g:"+f$:open #1,mb$:x=mark(1):close
 if not x ready " ":return
 kill "h:mbxtemp":create "h:mbxtemp":open #1,"h:mbxtemp":copy #8,#1:close
 print "Making Mailbox...";
 gosub msg
 open #1,"h:mbxtemp":edit(0):copy #1,#8:close:kill "h:mbxtemp"
 print ".done"\:ready " ":return

; create mail file
msg
 kill mb$:create mb$:open #1,mb$:ed=edit(5):fill ed,255,0:poke ed,4:poke ed+1,4
 write #1,ed,2:fill ed,255,0:for xz=1 to 4:write #1,ed,255:next
 write #1,ed,10:close #1:ready " "
 a$="T "+mb$+",BIN":use "b:xdos",a$
 return

; get a key
; l1$ is the list of available keys
getone
 print ": ";
getone.2
 get i$
 if instr(i$,l1$) return
 goto getone.2

; This routine gets a Yes/No answer
; A prompt is printed, hilighting the default
; response (if any)

; Input parameters are:

; yn	= 0:	No default
; yn	= 1:	Default is yes
; yn	= 2:	Default is no

getyn
 print " (";:l1$="YN"
 if yn:l1$=l1$+chr$(13)
 if yn=1 print "["on$"Y"of$"]";:else print "Y";
 print "/";
 if yn=2 print "["on$"N"of$"]";:else print "N";
 print ")";:gosub getone

 if i$=chr$(13):i$=mid$("YN",yn,1)
 if i$="Y" print "Yes":else print "No"
 yn=0:return

; show a disk file
show.file
 open #1,f$:if mark(1) close #1:return
 setint(1):print \s$\
showfl2
 copy (20) #1
 if (eof(1) or key(1)) setint(""):close #1:return
 if not flag(35) goto showfl2
 print "Press <"on$"RETURN"of$"> ";:l1$=chr$(13):gosub getone
 print chr$(8,17);chr$(32,17);chr$(8,17);
 if i$=" " setint(""):close #1:print:return
 setint(1):goto showfl2

; get time remaining before logoff
time
 x=(clock(2)-clock(1))/60:y=clock(2):x$=right$("0"+str$(x),2)
 if clock(1)>clock(2) x$="!!"
 if x=0 x$="--"
 if not y x$="**"
 if info(5) x$="::"
 return

; enter a message
editor
 on nocar goto editor1
 print \"Enter message now, "edit(3)" columns, 4k max"
 print "[DONE]  when finished,  [.H] for help"
 cl=clock(2):clock(2)=0:edit(0):edit(1):clock(2)=cl
 on nocar goto term1:return
editor1
 pop:clock(2)=cl:goto term1

; report an error
error
 close:on error:
 use "b:snapshot","b:snap.txt"
 print\sc$"A fatal error has occured. One moment, please..."\
 open #1,"b:error.list":position #1,32,err:input #1,er$:close
 f$="b:sys.errors":create f$:open #1,f$:append #1
 print #1,"Error #"err" ("er$") occurred in MAIN.SEG.S at "time$" on "date$"."
 print #1,a1$" "a2$ "(#"un") was online when this occured."
 open #2,"b:snap.txt":copy #2,#1
 print #1,"----------------------------------------------------------------"
 close: kill "b:snap.txt"
 goto term1
